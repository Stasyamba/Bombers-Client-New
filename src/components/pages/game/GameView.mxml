<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:ui="engine.ui.*"
         creationComplete="init()" width="100%" height="100%">

    <fx:Declarations>
        <!-- Place non-visual elements (e.g., services, value objects) here -->
    </fx:Declarations>
    <fx:Script><![CDATA[
        import components.ApplicationView
        import components.common.game.questgameresult.QuestResultObject
        import components.common.quests.medals.MedalType

        import engine.EngineContext
        import engine.games.GameType
        import engine.games.quest.medals.Medal
        import engine.ui.GameFieldView

        private var now:int,then:int;

        private var gameView:GameFieldView;
        private var _bg:Sprite;
        //temporary ui elements

        public function init():void {

        }

        public function everyGameTabOpeningInit():void {
            if (Context.gameModel.currentLocation) {
                if (_bg != null && bg.contains(_bg))
                    bg.removeChild(_bg);
                _bg = Context.imageService.asContentDisplay(Context.gameModel.currentLocation.stringId + ".map.background");
                bg.addChild(_bg);

            }
            switch (Context.gameModel.gameType) {
                case GameType.QUEST:
                    initQuest();
                    break
                case GameType.REGULAR:
                    initMultiPlayer();
                    break
                default:
                    throw new Error("unknown game type" + Context.gameModel.gameType.value)
            }
            subscribeEnterFrameEvent();
        }

        private function initMultiPlayer():void {
            //listener'll be removed in gameModel
            Context.gameModel.gameReady.add(function ():void {
                gameView = new GameFieldView(Context.game);
                addElement(gameView);
            });

            Context.gameModel.leftGame.add(function():void {
                endGame()
                while (hasEventListener(Event.ENTER_FRAME))
                    unsubscribeEnterFrameEvent();
            })
            //listener'll be removed in gameModel
            Context.gameModel.gameEnded.add(function ():void {
                endGame();
                Context.Model.dispatchCustomEvent(ContextEvent.GPAGE_NEED_TO_SHOW_GAME_RESULTS_WINDOW);
            });
        }

        private function initQuest():void {
            //listener'll be removed in gameModel
            Context.gameModel.questReady.add(function ():void {
                gameView = new GameFieldView(Context.game);
                addElement(gameView);
            });

            Context.gameModel.leftQuest.add(function():void {
                endGame()
                while (hasEventListener(Event.ENTER_FRAME))
                    unsubscribeEnterFrameEvent();
            })
            //listener'll be removed in gameModel
            Context.gameModel.questEnded.add(function (won:Boolean, medals:Array):void {
                endGame();
                //Context.Model.dispatchCustomEvent(ContextEvent.QUEST_END_SHOW, new QuestResultObject(won, won ? MedalType.byValue(medal.value):null))
                Context.Model.dispatchCustomEvent(ContextEvent.QUEST_END_SHOW, new QuestResultObject(won, medals.map(function(item:Medal, index:int, array:Array) {
                    return MedalType.byValue(item.value)
                })))
            });
        }


        private function endGame():void {
            if (gameView != null) {
                gameView.destroy();
                removeElement(gameView)
                gameView = null
            }
        }

        private function subscribeEnterFrameEvent():void {
            trace("subscribe")
            now = then = getTimer();
            addEventListener(Event.ENTER_FRAME, onEnterFrame)
        }

        private function onEnterFrame(e:Event):void {
            now = getTimer();
            var elapsed:int = now - then;
            then = now;
            EngineContext.frameEntered.dispatch(elapsed);
        }

        private function unsubscribeEnterFrameEvent():void {
            trace("unsubscribe")
            removeEventListener(Event.ENTER_FRAME, onEnterFrame)
        }

        //change tab back
        private function changeTabBack():void {
            Context.Model.dispatchCustomEvent(ContextEvent.NEED_TO_CHANGE_MAIN_TAB, ApplicationView.TAB_NAME_WORLD);
            if (hasEventListener(Event.ENTER_FRAME))
                unsubscribeEnterFrameEvent();
        }
        ]]></fx:Script>

    <!--<s:BorderContainer id="patternBorderContainer" width="100%" height="100%"-->
    <!--backgroundImage="@Embed(source='/assets/pagegame/grassPattern.jpg')"-->
    <!--backgroundImageFillMode="repeat"-->
    <!--borderVisible="false"/>-->
    <s:SpriteVisualElement id="bg"/>
</s:Group>
